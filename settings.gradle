def owPath = System.getenv("OPENWHISK_HOME") ?: '../incubator-openwhisk'
def owDirectory = new File(owPath)

if (!owDirectory.exists()) {
    throw new GradleScriptException(
        "Environment variable OPENWHISK_HOME must point to a valid OpenWhisk build", null)
}

include 'clitests'; project(':clitests').projectDir = new File(owDirectory, 'tests')

[   'common:scala',
    'core:controller',
    'core:invoker',
    'actionRuntimes:nodejs6Action',
    'actionRuntimes:actionProxy',
    'actionRuntimes:pythonAction',
    'actionRuntimes:python2Action',
    'actionRuntimes:javaAction',
    'actionRuntimes:swift3.1.1Action',
    'sdk:docker',
    'tests:dat:blackbox:badaction',
    'tests:dat:blackbox:badproxy'
].each() { p ->
    include p
    project(':'+p).projectDir = new File(owDirectory, p.replaceAll(':','/'))
}

include 'tests'

rootProject.name = 'openwhisk-cli'

gradle.ext.scala = [
    version: '2.11.8',
    compileFlags: ['-feature', '-unchecked', '-deprecation', '-Xfatal-warnings', '-Ywarn-unused-import']
]

gradle.ext.owPath = owPath
