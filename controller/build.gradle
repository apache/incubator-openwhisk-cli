apply plugin: 'scala'
apply plugin: 'application'
 
repositories {
    mavenCentral()
}

sourceSets {
    main {
        scala {
            srcDirs = ['src/']
        }
        resources {
            srcDirs = ['src/']
        }
    }
}

dependencies {
    compile project(':common:scala')
}

mainClassName = "whisk.core.controller.Controller"
applicationDefaultJvmArgs = ["-Xmx2g"]
startScripts {
    doLast {
        def lines = unixScript.readLines()
        lines[lines.size()-1] = lines.last() + ' >> /logs/${COMPONENT_NAME}_logs.log 2>&1'
        unixScript.text = lines.join("\n")
    }
}

def dockerImageName = project.name
def dockerRegistry =  project.hasProperty('dockerRegistry') ? dockerRegistry + '/' : ''
def dockerImageTag = project.hasProperty('dockerImageTag') ? dockerImageTag : 'latest'

task distDocker(dependsOn:[distTar, ':common:scala:distDocker'], type:Exec) {
    commandLine 'docker', 'build', '-t', 'whisk/' + dockerImageName, '.'
}
task tagImage(type: Exec) {
    commandLine 'docker', 'tag', '-f', 'whisk/' + dockerImageName, dockerRegistry + 'whisk/' + dockerImageName + ':' + dockerImageTag
}
task pushImage(dependsOn: tagImage, type:Exec) {
    commandLine 'docker', 'push', dockerRegistry + 'whisk/' + dockerImageName + ':' + dockerImageTag
}
pushImage.onlyIf { dockerRegistry != '' }
distDocker.finalizedBy pushImage